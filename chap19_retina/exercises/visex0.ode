# xppaut file for Exercise 3, Chapter 19, The Retina and Vision
#
# contrast=0 gives steady-states. 
# contrast=1 gives a stimulus of size I0 on a background of I0.
#
par I0=0.05 contrast=1
par s1=1.59  s2=1130  E=-13  Vstar=35.7
par tauy=0.07 k1=35.4 gam=303 delta=5 kappa=0.1
par eta=52.5 tau1=0.012 taum=0.016 tauz=0.04
# 
!V0=-s1*ln(1+s2*I0)
#
kernel(t)=(eta/tau1/6)*((t/tau1)^3)*exp(-t/tau1)
s(t)=eta*I0 + contrast*I0*kernel(t)/200
p'=s(t)*(1-p)-k1*p
x'=gfun(y)-(gam-delta)*x*p-delta*x
y'=((x^3)*exp(-V/Vstar)-y)/tauy
z'=(((1-kappa)/(1+kappa))*(x^3)*exp(-V/Vstar)\
   +(2*kappa/(1+kappa))*y-z)/tauz
V'=((-6*E/(4+kappa))*(x^3)*exp(-V/Vstar)+\
   (2*(1+kappa)/(4+kappa))*E*z-(V-E)-\
   (3*E*kappa/(4+kappa))*y)/taum
#
gfun(y)=(y*exp(E*(1-y)/Vstar))^(1/3)*\
        (delta+(gam-delta)*eta*\
        (exp(-E*(1-y)/s1)-1)/s2/\
        (k1+eta*(exp(-E*(1-y)/s1)-1)/s2))
#
# Here's A(y) (still called gfun just to make it easy).
# For part (d) use this gfun instead of the gfun above.
# gfun(y)=4+84/(1+(y/0.34)^4) 
#
# Can't put in the actual steady states as functions of I0
# so we just put in the dark steady state.
p(0)=0
x(0)=1
y(0)=1
z(0)=1
V(0)=0
#
# We choose to plot out the shifted voltages 
# (which is why we bother to calculate V0 at all)
aux Vshift=V-V0
@ yp=Vshift total=2.5 xlo=0 xhi=0.5 ylo=-0.5 yhi=0.1 dt=0.001
@ ntst=50 nmax=2000 npr=2000 dsmin=0.0001 dsmax=0.01 ds=0.001 parmin=0.00001
@ parmax=1 autoxmin=0.00001 autoxmax=1 autoymin=-12 autoymax=0 autovar=v
