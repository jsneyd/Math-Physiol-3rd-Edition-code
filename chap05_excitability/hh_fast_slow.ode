#   ------------------------------------------------------------------- 
#    This is a simple ode integrator for the reduced HH equations (also called
#    the fast-slow HH equations). 
#
#    For Chapter 5, Fig. 5.10 of
#    Keener and Sneyd, Mathematical Physiology, 3rd Edition, Springer.
#  
#    Written by James Keener and James Sneyd
#  
#   ------------------------------------------------------------------- 

# initialize the variables. v is the deviation from rest.
v(0)=-0.19571
n(0)=0.31468

# define the parameters and functions
par Iapp=0.0
par gna=120.0, gk=36.0,gl=0.3
par vna=115.0, vk=-12.0, vl=10.6
par cm=1.0

alpham=0.1*(25.0-v)/(exp((25.0-v)/10.0)-1.0)
betam=4.0*exp(-(v)/18.0)
alphan=0.01*(10.0-v)/(exp((10.0-v)/10.0)-1.0)
betan=0.125*exp(-(v)/80.0)
m=alpham/(alpham + betam)

# define the ODE's
v'=(1.0/cm)*(-gk*(n^4)*(v-vk) - gna*(m^3)*(0.8-n)*(v-vna) - gl*(v-vl) + Iapp)
n'=alphan*(1.0-n) - betan*n

@ xpar=v, ypar=n,xlo=-0.3
@ ylo=0,yhi=1,bounds=1000,deltat=0.01,xlo=-20,xhi=120,total=100
@ autovar=Iapp,ntst=50,nmax=200,npr=200,dsmin=0.1,dsmax=2,ds=0.2
@ parmin=-10,parmax=400
@ autoxmin=0,autoxmax=400,autoymin=-20,autoymax=120
done