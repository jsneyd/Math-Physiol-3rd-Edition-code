# Initial conditions are the steady state for p=0.4, ct=1. No oscillations
#
c(0)=0.3
y(0)=0.47
p(0)=0.4
#
# Parameter values
#
# this is the positive feedback version of politi et al.

param Vplc=0
param ct=10

k1=400
km1=52
k2=0.2
km2=0.21
k3=400
km3=377.2
k4=0.2
km4=0.029
k5=20
km5=1.64

bigK1=km1/k1
bigK2=km2/k2
bigK3=km3/k3
bigK4=km4/k4
bigK5=0.08

gamma = 5.5

param kf=1.1
k3K =0
k5P=0.66
Kplc=0.2
Vserca = 0.9
Kserca=0.1
kf2=0.0203
Ki=0.4
tauy=12.5

#
# Define the functions used in the model
#

ce=ct-gamma*c
Po= ( p*c*(1-y)/((p+bigK1)*(c+bigK5)) )^3

Jserca=Vserca*(c*c)/(c*c+Kserca*Kserca)

#
# Define the differential equations here
# 
dc/dt = (kf*Po+kf2)*(ce-c)-Jserca
dy/dt = (-1+(1-y)*(Ki+c)/Ki)/tauy
dp/dt =(Vplc*c^2/(Kplc^2+c^2)- (k3K+k5P)*p)

#
@ total=400,dt=0.05,xp=t,yp=c,xlo=0,xhi=400,ylo=0,yhi=1,meth=gear,bounds=1000
@ ntst=250,nmax=200000,npr=200000,dsmin=0.001,dsmax=0.05,ds=0.002
@ parmin=0,parmax=4
@ autoxmin=0,autoxmax=4,autoymin=0,autoymax=1,nmax=20000
@ epsl=1e-8, epsu=1e-8, epss=1e-8

done