#  -------------------------------------------------------------------
#
#   A closed-cell model for calcium oscillations based on the modal model of the IP3R.
#
#   For Chapter 7, Section 7.5.2 of
#   Keener and Sneyd, Mathematical Physiology, 3rd Edition, Springer.
# 
#   Written by James Keener and James Sneyd.
# 
#  ------------------------------------------------------------------- 


# Initial conditions are the steady state for p=0
#
c(0)=0.073622
h(0)=0.58233

# Parameter values

param ct = 2
param p = 0.2
param tau_max=1000
param Ktau=0.1
param tauP=1
param Kc=0.2, Kh=0.08
param kf=10
 
Vserca=0.9
kserca=0.2
Kbar=0.00001957
Kp=0.2
gamma=5.5

# Define the functions used in the model

ce=gamma*(ct-c)

phi_c=c^4/(c^4+Kc^4)
phi_p=p^2/(Kp^2+p^2)
phi_p_down=Kp^2/(Kp^2+p^2)

h_inf=Kh^4/(Kh^4+c^4)
tau = tau_max*Ktau^4/(Ktau^4+c^4)
serca=Vserca*(c*c-Kbar*ce*ce)/(c*c+kserca*kserca)

beta = phi_p*phi_c*h
alpha = phi_p_down*(1-phi_c*h_inf)
Po=beta/(beta + 0.4*(beta+alpha))


# Define the differential equations here
# The parameter scaling just fiddles the time scale
#
dc/dt  = kf*Po*(ce-c)-serca
dh/dt  = (h_inf-h)/tau
#

@ total=200,dt=0.05,xp=t,yp=c,xlo=0,xhi=200,ylo=0,yhi=1,meth=gear,bounds=1000
@ ntst=25,dsmin=0.001,ds=0.002,dsmax=0.005,parmax=5
@ nmax=5000, npr=5000
@ autoxmin=0,autoxmax=5,autoymin=0,autoymax=1.0
done