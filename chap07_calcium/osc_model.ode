# Initial conditions are the steady state for p=0
#
c(0)=0.013919
ct(0)=12.157
R(0)=0.87209
O(0)=0.0
A(0)=0.0
I1(0)=0.12791
I2(0)=0.0
#
# Parameter values
#
param p=0.0,alpha1=0.03
param delta=0.1,alpha2=0.2,Vp=28.0,Kp=0.425
param gamma=5.405,g1=0.002,kflux=0.96,Vs=120.0,Ks=0.18
param k2=37.4,k22=1.4,k3=0.11,k33=29.8,k4=4,k44=0.54,L1=0.12,k1=0.64,k11=0.04
param L3=0.025,L5=54.7,l2=1.7,l4=1.7,l6=4707,l22=0.8,l44=2.5,l66=11.4
#
!Jinflux=alpha1+alpha2*p
#
# Define the functions used in the model
#
F1(c)=((k1*L1+l2)*c)/(L1+c*(1+L1/L3))
F2(c)=(k2*L3+l4*c)/(L3+c*(1+L3/L1))
F22(c)=(k22+l44*c)/(1+c/L5)
F3(c)=(k3*L5)/(L5+c)
F4(c)=((k4*L5+l6)*c)/(L5+c)
F44(c)=(L1*(k44+l66))/(L1+c)
F5(c)=((k1*L1+l2)*c)/(L1+c)
serca(c,dum)=((Vs*c)/(Ks+c))*(1.0/dum)
pmca(c)=(Vp*c^2)/(Kp^2+c^2)
ce(c,ct)=gamma*(ct-c)
#
# Define the differential equations here
#
dc/dt = (kflux*(0.1*O+0.9*A)^4+g1)*(ce(c,ct)-c)+delta*(Jinflux-pmca(c))-serca(c,ce(c,ct))
dct/dt = delta*(Jinflux-pmca(c))
dR/dt = F22(c)*O-F2(c)*p*R+(k11+l22)*I1-F1(c)*R
dO/dt = F2(c)*p*R-(F22(c)+F4(c)+F3(c))*O+F44(c)*A+k33*(1.0-R-O-A-I1-I2)
dA/dt = F4(c)*O-F44(c)*A-F5(c)*A+(k11+l22)*I2
dI1/dt = F1(c)*R-(k11+l22)*I1
dI2/dt = F5(c)*A-(k11+l22)*I2
#
# Stuff that might be of interest
#
aux ipflux=(kflux*(0.1*O+0.9*A)^4+g1)*(ce(c,ct)-c)
aux sercaflux=serca(c,ce(c,ct))
aux memflux=delta*(Jinflux-pmca(c))
aux cer=ce(c,ct)
#
@ total=200,dt=0.05,xl0=0,xhi=200,ylo=0,yhi=2.2,meth=gear,bounds=1000
@ autovar=c,ntst=50,nmax=2000,npr=2000,dsmin=0.01,dsmax=0.05,ds=0.02,parmin=0,parmax=60
@ autoxmin=0,autoxmax=50,autoymin=0,autoymax=2
done