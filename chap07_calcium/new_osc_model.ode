# Initial conditions are the steady state for p=0
#
c(0)=0.013919
ct(0)=9.5513
R(0)=0.87209
O(0)=0.0
A(0)=0.0
I1(0)=0.12791
I2(0)=0.0
#
# Parameter values
#
param p=0,a1=0.003
param delta=1.0,a2=0.02,Vp=2.80,Kp=0.425
param gamma=5.405,Jer=0.002,kflux=0.96

#param k2=37.4,k22=1.4,k3=0.11,k33=29.8,k4=4,k44=0.54,L1=0.12,k1=0.64,k11=0.04
#param L3=0.025,L5=54.7,l2=1.7,l4=1.7,l6=4707,l22=0.8,l44=2.5,l66=11.4

param al1=0.3,b1=0.02,al2=0.77,b2=1.38,al22=76.6,b22=137
param al3=6,b3=54.7,al4=4926,al44=1.43,al5=1.78,b5=0.12
param k11=0.84,l22=0, k33=29.8

param alpha1=1e-4,alpha2=0.007,alpha3=0.06,alpha4=0.0014,alpha5=0.007

#
!Jinflux=a1+a2*p
#
# Define the functions used in the model
#
#F1(c)=((k1*L1+l2)*c)/(L1+c*(1+L1/L3))
#F2(c)=(k2*L3+l4*c)/(L3+c*(1+L3/L1))
#F22(c)=(k22+l44*c)/(1+c/L5)
#F3(c)=(k3*L5)/(L5+c)
#F4(c)=((k4*L5+l6)*c)/(L5+c)
#F44(c)=(L1*(k44+l66))/(L1+c)
#F5(c)=((k1*L1+l2)*c)/(L1+c)

F1(c)=al1*c/(b1+c)
F3(c)=al3/(b3+c)
F5(c)=al5*c/(b5+c)
F2(c)=(al2+b2*c)/(b1+c)
F22(c)=(al22+b22*c)/(b3+c)
F4(c)=al4*c/(b3+c)
F44(c)=al44/(b5+c)

serca(c,dum)=(c-alpha1*dum)/(alpha2+alpha3*c+alpha4*dum+alpha5*c*dum)
pmca(c)=(Vp*c^2)/(Kp^2+c^2)
ce(c,ct)=gamma*(ct-c)
#
# Define the differential equations here
#
dc/dt = (kflux*(0.1*O+0.9*A)^4+Jer)*(ce(c,ct)-c)+delta*(Jinflux-pmca(c))-serca(c,ce(c,ct))
dct/dt = delta*(Jinflux-pmca(c))


dR/dt = F22(c)*O-F2(c)*p*R+(k11+l22)*I1-F1(c)*R
dO/dt = F2(c)*p*R-(F22(c)+F4(c)+F3(c))*O+F44(c)*A+k33*(1.0-R-O-A-I1-I2)
dA/dt = F4(c)*O-F44(c)*A-F5(c)*A+(k11+l22)*I2
dI1/dt = F1(c)*R-(k11+l22)*I1
dI2/dt = F5(c)*A-(k11+l22)*I2
#
# Stuff that might be of interest
#
aux ipflux=(kflux*(0.1*O+0.9*A)^4+Jer)*(ce(c,ct)-c)
aux sercaflux=serca(c,ce(c,ct))
aux memflux=delta*(Jinflux-pmca(c))
aux cer=ce(c,ct)
#
@ total=200,dt=0.05,xl0=0,xhi=200,ylo=0,yhi=2.2,meth=gear,bounds=1000
@ autovar=c,ntst=50,nmax=2000,npr=2000,dsmin=0.01,dsmax=0.05,ds=0.02,parmin=0,parmax=60
@ autoxmin=0,autoxmax=50,autoymin=0,autoymax=2
done