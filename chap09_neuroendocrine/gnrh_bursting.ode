#  -------------------------------------------------------------------
#
#   Solve the model of bursting in GnRH neurons.
#   Simplified version of the model of Duan et al, 
#           Journal of Theoretical Biology 276 (2011) 22â€“34
#
#   For Chapter 9, Section 9.2 of
#   Keener and Sneyd, Mathematical Physiology, 3rd Edition, Springer.
# 
#   Written by James Keener and James Sneyd.
# 
#  ------------------------------------------------------------------- 

V(0) =  -47.439744033440896
Hnaf(0) =  0.150875059704950
Nkm(0) =   0.068499547036174
c(0) =   0.042896908631945
ce(0) =   10
h(0) =   0.061113223260552
Ow(0) =   0.000000002523211
Ow_star(0) =   0.000049694789395


par IP3=0.15

# Ca and IPR parameters
tau_max=10
Ktau=0.1
Kc=0.2
Kh=0.08
Vserca=0.1
kserca=0.2
Kbar=0.00001957
Vp=0.0042 
Kf=1
Kp=0.425
delta=0.5  
gamma=27  
alpha0=4.8e-3
alpha1=2e-5 

# conductances
gnaf= 280
gsk=0.3
gw=950
gcal=0.05 
gkm=8  
gleak=0.04

# resting potentials 
number Cm=16  Vna=60  Vca=100  Vk=-80  Vleak=100 	

# Isk and Iw channel parameters						
number k_sk=1  n_sk=2  k11=1e-7  k_11=1.2  k22=0.5  k33=3e-5 	


# Current submodel
Mnaf_inf = 1/(1+exp(-(V+40.0)/4.3))
Hnaf_inf = 1/(1+exp((V+66.1)/10.8))
Nkm_inf = 1/(1+exp(-(V+37)/4))
Mcal_inf = 1/(1+exp(-(V+30)/2))

Thnaf = 75/(exp((V+80)/19)+2*exp(-2*(V+80)/19))
Tnkm = 11.5/(exp((V+30)/15)+exp(-(V+30)/15))

Inaf =  gnaf*Mnaf_inf^3*Hnaf*(V- Vna)
Ical =  gcal*Mcal_inf^2*(V- Vca)
Ileak =  gleak*(V- Vleak)

Ikm =  gkm*Nkm*(V- Vk)
Isk =  gsk*(c^( n_sk)/(c^( n_sk)+ k_sk^( n_sk)))*(V- Vk)
Iw =  gw*(Ow+Ow_star)*(V- Vk)

Iionic = Inaf+Ikm+Ical+Ileak

# calcium submodel

phi_c=c^4/(c^4+ Kc^4)
phi_p= IP3^2/( Kp^2+ IP3^2)
phi_p_down= Kp^2/( Kp^2+ IP3^2)

h_inf= Kh^4/( Kh^4+c^4)
tau =  tau_max* Ktau^4/( Ktau^4+c^4)
beta_IPR = phi_p*phi_c*h
alpha_IPR = phi_p_down*(1-phi_c*h_inf)
Po=beta_IPR/(beta_IPR + 0.4*(beta_IPR+alpha_IPR))

JIPR = Kf*Po*(ce-c)
Jserca = Vserca*(c*c-Kbar*ce*ce)/(c*c+kserca*kserca)
Jin = - alpha0*(Ical)+ alpha1*IP3
Jpm =  Vp*c^2/( Kp^2+c^2)


aux XX = Ow + Ow_star
aux tsec = t/1000


########## Differential equations ######

V' = (-1/ Cm)*(Iionic+Isk+Iw)
Hnaf' = (Hnaf_inf-Hnaf)/Thnaf
Nkm' = (Nkm_inf-Nkm)/Tnkm
c' =  delta*(Jin - Jpm) + (JIPR - Jserca) 

# the factor of 1/1000 is because time is in ms, not s
h'  = 0.001*(h_inf-h)/tau   
 
ce' =  gamma*(Jserca - JIPR)
Ow' = -Ow* k_11-Ow* k22+ k11*c*(1-Ow-Ow_star)
Ow_star' =  k22*Ow- k33*Ow_star

##### XPPAUT parameters

@ XLO=52,XHI=58,YLO=-85,YHI=45,        
@ BOUND=30000,TOTAL=60000,NOUT=1,METH=gear,dt=1,MAXSTOR=1000000               
@ NPLOT=1,XP1=tsec,YP1=V               
@ NTST=75,DSMIN=1e-5,DSMAX=.5,DS=0.01        
@ parmin=0,parmax=5,autoxmin=0,autoxmax=1 autoymin=-80,autoymax=40            
@ EPSU=1e-7, EPSL=1e-7, EPSS=1e-5                   
@ Nmax=100000, NPr=10000    
