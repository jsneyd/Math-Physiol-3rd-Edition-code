# ----------------------------
#  Slightly modified Chay-Keizer model of bursting. 
#  Original version from Artie Sherman.
#  Biophys. J. 42:181-190 '83
#
# For Chapter 9, Section 9.1.1, of
# Keener and Sneyd, Mathematical Physiology, 3rd Edition, Springer
#
# Written by James Keener and James Sneyd.
# ----------------------------

# Initial values:
v(0)    = -54.774  
ca(0)   = 0.10749  
mca(0)  = 0.027532  
hca(0)  = 0.086321  
n(0)    = 0.00044035  


# Parameters:
# conductances in mS/cm^2, ca in uM, v in mV, t in msec
p cm=4.5 gca=13 gk=12 gl=0.04 gkca=0.09
p vca=100 vk=-75 vl=-40
p vprime=50 vstar=30
p f=0.01 k1=0.01525
p Kd=1 kca=0.04 

# Gating functions:
alphamca(v)   = -0.1*((v+vprime)-25)/(exp(-((v+vprime)-25)/10)-1)
betamca(v)    = 4*exp(-(v+vprime)/18)
alphahca(v)   = 0.07*exp(-(v+vprime)/20)
betahca(v)    = 1/(exp(-((v+vprime)-30)/10)+1)
alphan(v)     = -0.01*((v+vstar)-10)/(exp(-((v+vstar)-10)/10)-1)
betan(v)      = 0.125*exp(-(v+vstar)/80)

# Ionic currents:
i_ca  = gca*mca^3*hca*(v-vca)
i_k   = gk*n^4*(v-vk)
i_kca = gkca*ca/(ca+Kd)*(v-vk)
i_l   = gl*(v-vl)

# The differential equations:
v'    = -1/cm*(i_ca+i_k+i_kca+i_l) 
mca'  = (alphamca(v)*(1-mca)-betamca(v)*mca)
hca'  = (alphahca(v)*(1-hca)-betahca(v)*hca) 
n'    = (alphan(v)*(1-n)-betan(v)*n)  
ca'   = f*(-k1*i_ca-kca*ca)

aux tsec=t/1000

# Numerical and plotting parameters for xpp:
@ maxstor=1000000, meth=cvode, xp=tsec,yp=v,toler=1e-6, atoler=1e-6
@ xlo=0, xhi=15, ylo=-70, yhi=-10, total=15000, bounds=100000, dt=1

done
