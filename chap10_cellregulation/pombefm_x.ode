#constant mass
# pombe3.ode-bol de IE GK
# mass a cyclin szintezisen
init CycBT=1.50724, pMPF=0.04434, Cdh1=0.00021, Cdc20T=0.82428, Cdc20A=0.01049,
init IE=0.25392, CKIT=0.01945, SK=0.78888, x=0
#, M=1

CycBT' = k1*M - (k2'+k2"*Cdh1+k2'"*Cdc20A)*CycBT
pMPF'  = kwee*(CycBT-pMPF) - k25*pMPF - (k2'+k2"*Cdh1+k2'"*Cdc20A)*pMPF
Cdh1'  = (k3'+k3"*Cdc20A)*(1-Cdh1)/(J3+1-Cdh1) -(k4'*SK+k4*MPF)*Cdh1/(J4+Cdh1)
Cdc20T'= k5' + k5"*MPF^4/(J5^4+MPF^4) - k6*Cdc20T
Cdc20A'= k7*IE*(Cdc20T-Cdc20A)/(J7+Cdc20T-Cdc20A) - k8*Cdc20A/(J8+Cdc20A)-k6*Cdc20A
IE'    = k9*(1-IE)*MPF/(J9+1-IE) - k10*IE/(J10+IE)
CKIT'  = k11 - (k12+k12'*SK+k12"*MPF)*CKIT
SK'    = k13' + k13"*TF - k14*SK
#M'     = mu*M
dx/dt=K*(MPF-x)

BB     = CycBT+CKIT+Kdiss
Trimer = 2*CycBT*CKIT/(BB+sqrt(BB^2-4*CycBT*CKIT))
MPF   = (CycBT-pMPF)*(CycBT-Trimer)/CycBT

GB(arg1,arg2,arg3,arg4) = arg2-arg1+arg2*arg3+arg1*arg4
GK(arg1,arg2,arg3,arg4) =2*arg1*arg4/(GB(arg1,arg2,arg3,arg4)+sqrt(GB(arg1,arg2,arg3,arg4)^2-4*(arg2-arg1)*arg1*arg4))
TF     = GK(k15'*M+k15"*SK,k16'+k16"*MPF,J15,J16)
kwee   = kwee' + (kwee"-kwee')*GK(Vawee,Viwee'+Viwee"*MPF,Jawee, Jiwee)
k25    = k25' + (k25"-k25')*GK(Va25'+Va25"*MPF,Vi25,Ja25,Ji25)

#global -1 {MPF-0.1} {M=0.5*M}

aux MPF = (CycBT-pMPF)*(CycBT-Trimer)/CycBT

p M=0.,K=10

p k1=.03,k2'=.03,k2"=1,k2'"=.1
p k3'=1,k3"=10,k4'=2,k4=35
p k5'=.005,k5"=.3,k6=.1
p J3=.01,J4=.01,J5=.3
p k7=1,k8=.25,J7=.001,J8=.001
p k9=.1,k10=.04, J9=0.01, J10=0.01
p k11=.1,k12=0.01,k12'=1,k12"=3,Kdiss=.001
p k13'=0,k13"=.1,k14=.1
p k15'=1.5,k15"=0,k16'=1,k16"=2,J15=.01,J16=.01
p mu=.005,Mstar=10
p kwee'=0.15, kwee"=1.3, Vawee=0.25, Viwee'=0, Viwee"=1, Jawee=0.01, Jiwee=0.01
p k25'=0.05, k25"=5, Va25'=0, Va25"=1, Vi25=0.25, Ja25=0.01, Ji25=0.01


@ YP=M, TOTAL=150, METH=stiff, XHI=200, YLO=0, YHI=2, BOUND=1000
done

